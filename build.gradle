import edu.wpi.first.gradlerio.frc.RoboRIO
import edu.wpi.first.gradlerio.frc.FRCJavaArtifact
import java.text.SimpleDateFormat

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.10"
    id "edu.wpi.first.GradleRIO" version "2018.06.21"
}

ext.kotlinVersion = "1.3.10"
ext.sertainVersion = "1.2.1"
ext.ktlintVersion = "0.19.0"
ext.gsonVersion = "2.8.5"
ext.meanlibVersion = "cb8ced04b8be85a6518f8353ec08624a2ca2681c"

check.dependsOn "ktlint"
compileKotlin.dependsOn "versionTxt"
tasks.whenTaskAdded { task ->
    if (task.name == "deploy" || task.name == "deployMain") task.dependsOn "assemble"
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

configurations {
    ktlint
}

wpi {
    wpilibVersion = "2018.4.1"
    ntcoreVersion = "4.1.0"
    opencvVersion = "3.2.0"
    cscoreVersion = "1.3.0"
    wpiutilVersion = "3.2.0"

    ctreVersion = "5.5.1.0"
    navxVersion = "3.0.348"

    shuffleboardVersion = "1.3.1"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    compile "com.github.SouthEugeneRoboticsTeam:meanlib:$meanlibVersion"
//    compile project("meanlib")
    compile "org.sert2521.sertain:core:$sertainVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0"

    compile wpilib()
    compile navx()
    compile ctre()

    ktlint "com.github.shyiko:ktlint:$ktlintVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

deploy {
    targets {
        target("roborio", RoboRIO) {
            team = getTeamOrDefault(2521)
        }
    }
    artifacts {
        artifact("main", FRCJavaArtifact) {
            targets << "roborio"

            debug = getDebugOrDefault(false)
        }
    }
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes(
                "Main-Class": "org.sert2521.bunnybots.MainKt"
        )
    }
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--abbrev-ref", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitFilesChanged = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "diff", "--name-only", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim().replace("\n", ", ")
}

task versionTxt()  {
    doLast {
        String resourcesDir = "$projectDir/src/main/resources"
        new File("$resourcesDir/branch.txt").text = getGitBranch()
        new File("$resourcesDir/commit.txt").text = getGitHash()
        new File("$resourcesDir/changes.txt").text = getGitFilesChanged()
        new File("$resourcesDir/buildtime.txt").text =
                new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())
    }
}

wrapper {
    gradleVersion = "4.10"
}
